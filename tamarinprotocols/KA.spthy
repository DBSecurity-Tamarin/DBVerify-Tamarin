/* Implementation of the K-A distance bounding protocol */
/* Demonstrating the DB-secure property */

theory ka

begin

functions: f/2, h/1

// -------------------- Tag Initialization -------------------- //

    // Rule for registering a prover/verifier. We create "agent" facts
    // Because the verifier doesn't necessarily know his communication partner
    // When he sends his first message (but we want to track V's session)
rule Register_Keypair:
  [ Fr(~key) ]
--[ KeyGen($A, $V, ~key) ,Once($A, $V), Prelim()]->
  [ !SharedKey($A, $V, ~key), !Agent($A), !Agent($V) ]

// -------------------- Corrupted Agent Rules ---------------- //

    // There's a small difference between a corrupted prover and a corrupted
    // verifier, that comes into play when we make our security claim later.
    // This is a consequence of differences between prover impersonation and
    // verifier impersonation attacks

rule Corrupt_Prover:
  [ !SharedKey($A, $V, key) ]
--[ Corrupted($A, $V), Prelim(), Corrupt($A) ]->
  [ Out(key), !CorruptAgent($A) ]

rule Corrupt_Verifier:
  [ !SharedKey($A, $V, key) ]
--[ Corrupt($V), Prelim() ]->
  [ Out(key) ]

    // The adversary can only interact with the network using the In() and Out() facts:
    // The FastMsg fact is used for messages sent during the fast phase.
    // If the adversary wants to send a message in the fast phase, they have to use a corrupted
    // prover as a "voice"
rule Corrupt_Fast:
  [ !CorruptAgent($A), In(msg) ]
--[ CAction($A), Action($A) ]->
  [ FastMsg(msg) ]

// -------------------- Protocol Rules -------------------- //

// The prover initiates conversation by sending a nonce

rule Prover_1:
  [ Fr(~nb), !Agent($P) ]
--[ ProverOne($P, ~nb), Action($P) ]->
  [ Out( ~nb ), ProverStart($P, ~nb) ]

// The verifier replies by sending another nonce

rule Verifier_1:
  [ Fr( ~na ), In( nb ) ]
--[ VerifierOne(~na, nb)]->
  [ Out( ~na ), VerifierStart(~na, nb)]

// The verifier builds a challenge and sends it

rule Verifier_2:
  [ VerifierStart(na, nb), Fr(~challenge) ]
--[ StartFastPhase(na, nb, ~challenge) ]->
  [ Out(~challenge), VerifierChallenge(na, nb, ~challenge) ]

// The prover replies with a value built from all the nonces
// so far, as well as a shared secret key

rule Prover_2:
  [ In(challenge), In(na), ProverStart($P, nb), !SharedKey($P, $V, key) ]
--[ ProverTwo($P, $V, na, nb, challenge), Action($P)]->
  [ FastMsg(h( <key, na, nb, challenge> ))  ]

// The verifier ends the fast phase and checks everything lines up

rule Verifier_3:
  [ FastMsg(h(<key, na, nb, challenge>)), !SharedKey($P, $V, key), VerifierChallenge(na, nb, challenge) ]
--[ EndFastPhase(na, nb, challenge), VerifierComplete($P, $V, na, nb, challenge) ]->
  []

// ------------------- Axioms -------------------------- //

  // A given pair of agents can only have keypair between them

axiom at_most_once:
    "
    All A V #t1 #t2. Once(A, V)@t1 & Once(A, V)@t2 ==>
        (
        #t1 = #t2
        )
    
    
    "

  // Key initialization rules always take place before protocol rules
  // This restriction is not necessary, but helps tidy up the trace
  // and slightly tightens the security requirement
  
axiom prelims_first:
    "
    All A #t1 #t2. Prelim()@t1 & Action(A)@t2 ==>
    (
        t1 < t2
    )

    "    

// -------------------- Sanity Check Lemmas -------------------- //

  // These lemmas check that certain points in the trace are reachable.
  // They're useful to check that the protocol has been correctly implemented
  // but are not necessary for the analysis

lemma verif1_exists:
    exists-trace
  "
    Ex na nb #t. (
        VerifierOne(na, nb)@t )
  "

lemma prover1_exists:
    exists-trace
  "
    Ex P nb #t. (
        ProverOne(P, nb)@t )

  "

lemma verif2_exists:
    exists-trace
  "
    Ex na nb challenge #t. (
        StartFastPhase(na, nb, challenge)@t )

  "

lemma prover2_exists:
    exists-trace
  "
    Ex P V m n c #t. (
        ProverTwo(P, V, m, n, c)@t )

  "

lemma finish_exists:
    exists-trace
  "
   ( Ex P V m n c #t. (
        VerifierComplete(P, V, m, n, c)@t )
   ) &
	not( Ex A B #t1. Corrupted(A, B)@t1 )
  "

// -------------------- Security Lemmas -------------------- //

lemma dbsec:
  "
  All P V m n c #t. (
    VerifierComplete(P, V, m, n, c)@t ) ==>                   // Whenever the verifier is complete EITHER:
    (Ex #tc. Corrupt(V)@tc)|                                  //  1) The verifier is corrupt OR
    (                                                         
      Ex #t1 #t2 #t3. StartFastPhase(m, n, c)@t1 &              //  2) There exists t1, t2 and t3 such that
                  Action(P)@t2 &                              //          The fast phase was between t1 and t3
                  EndFastPhase(m, n, c)@t3 &            //          and the prover did something between those two times
                  (#t1 < #t2) &                               //          and the verifier completed at or after the
                  (#t2 < #t3) &                               //          end of the fast phase OR
                  ( (#t3 < #t ) | (#t3 = #t) )
    )|
    (              
      Ex CAgent #t4 #t5 #t6 #t7. StartFastPhase(m, n, c)@t5 &    //  3) There exist times t5, t6 and t7 such that
                             EndFastPhase(m, n, c)@t7 & //        The fast phase was between t5 and t7
                             Corrupted(P, V)@t4 &             //        and the prover P is dishonest
                             CAction(CAgent)@t6 &             //        and some dishonest agent did something during
                             (#t5 < #t6)&                     //        the fast phase
                             (#t6 < #t7)&                     //        and the verifier completed at or after the
                             ( (#t7 < #t) | (#t7 = #t) )      //        end of the fast phase 
    ) 
  "


end
